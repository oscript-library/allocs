
#Использовать 1commands
#Использовать fs

Процедура Подготовить() Экспорт

	ИменаФайловБиблиотек = Новый Соответствие();
	ИменаФайловБиблиотек.Вставить("1script_allocs.dll");

	СобратьБиблиотекуDotNET();
	ПодготовитьКаталогСКомпонентами(ИменаФайловБиблиотек);
	ПроверитьНаличиеБиблиотекиDotNET(ИменаФайловБиблиотек);

КонецПроцедуры

Процедура СобратьБиблиотекуDotNET()

	КаталогРелиза = ОбъединитьПути(ТекущийКаталог(), "src/bin/Release");
	ФС.ОбеспечитьПустойКаталог(КаталогРелиза);

	Команда = Новый Команда;
	Команда.УстановитьСтрокуЗапуска("dotnet build src -c Release");
	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
	
	КодВозврата = Команда.Исполнить();
	Если Не КодВозврата = 0 Тогда
		ВызватьИсключение "Не удалось выполнить сборку .NET библиотеки";
	КонецЕсли;

КонецПроцедуры

Процедура ПодготовитьКаталогСКомпонентами(ИменаФайловБиблиотек)

	СоответствиеПапок = Новый Соответствие();
	СоответствиеПапок.Вставить("net4", "net48");
	СоответствиеПапок.Вставить("dotnet", "net8.0");
	
	КаталогСКомпонентами = ОбъединитьПути(ТекущийКаталог(), "Components");

	ФС.ОбеспечитьПустойКаталог(КаталогСКомпонентами);

	Для Каждого Соответствие Из СоответствиеПапок Цикл
		ИмяКаталогаOscript = Соответствие.Ключ;
		ИмяКаталогаDotnet = Соответствие.Значение;

		ПутьККаталгуOscript = ОбъединитьПути(КаталогСКомпонентами, ИмяКаталогаOscript);
		ФС.ОбеспечитьПустойКаталог(ПутьККаталгуOscript);

		ПутьККаталгуDotnet = ОбъединитьПути(ТекущийКаталог(), "src/bin/Release", ИмяКаталогаDotnet);

		Для Каждого Строка Из ИменаФайловБиблиотек Цикл
			ИмяФайла = Строка.Ключ;
			ПутьИсточник = ОбъединитьПути(ПутьККаталгуDotnet, ИмяФайла);
			ПутьПриемник = ОбъединитьПути(ПутьККаталгуOscript, ИмяФайла);

			Если ФС.ФайлСуществует(ПутьИсточник) Тогда
				ПереместитьФайл(ПутьИсточник, ПутьПриемник);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьНаличиеБиблиотекиDotNET(ИменаФайловБиблиотек)

	Для Каждого ИмяПапки Из СтрРазделить("net4,dotnet", ",") Цикл
		Для Каждого Строка Из ИменаФайловБиблиотек Цикл
			
			ИмяФайла = Строка.Ключ; 
			ИмяЦелевойПапки = Строка.Значение;

			Если ЗначениеЗаполнено(ИмяЦелевойПапки) И ИмяЦелевойПапки <> ИмяПапки Тогда
				Продолжить;
			КонецЕсли;

			ПутьКФайлу = ОбъединитьПути(ТекущийКаталог(), "Components", ИмяПапки, ИмяФайла);
			Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
				ВызватьИсключение СтрШаблон("Отсутсвует .NET библиотека %1 в папке Components/%2", ИмяФайла, ИмяПапки);
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

КонецПроцедуры